# Contributor: Weng Xuetian <wengxt@gmail.com>

pkgname=firefox-kde-opensuse
pkgver=4.0
pkgrel=3
_xulver=2.0
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=("xulrunner-kde-opensuse=${_xulver}" 'desktop-file-utils')
makedepends=('zip' 'pkg-config' 'diffutils' 'python2' 'wireless_tools' 'yasm' 'mesa')
provides=("firefox=${pkgver}")
conflicts=('firefox')
install=firefox.install
url="http://www.mozilla.org/projects/firefox"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
        mozconfig
        firefox.desktop
        firefox-safe.desktop
        mozilla-firefox-1.0-lang.patch
        firefox-version.patch
        kde.js
        firefox-cross-desktop.patch
        firefox-kde.patch
        firefox-browser-css.patch
        firefox-kde-114.patch
)

md5sums=('3468a2c463b4fc2788ba621e4b511c30'
         'da3221429293fe756a9a4312173beb10'
         'ba96924ece1d77453e462429037a2ce5'
         '6f38a5899034b7786cb1f75ad42032b8'
         'bd5db57c23c72a02a489592644f18995'
         'cea73894617d0e12362db294864fb87f'
         '75df0f88cc7a7fa7d522459e4ff82cc5'
         'ef2ec32d2c98fbbdc0304801a3d6413d'
         '48fd3dab87e73cbfdd66e3c35fc0c79b'
         '36868e98e05c27c9cb5b982d386b6dea'
         '038666a4be4e56c1ea25265fa4a12dd2')

build() {
  cd "${srcdir}/mozilla-2.0"
  patch -Np1 -i "${srcdir}/mozilla-firefox-1.0-lang.patch"
  patch -Np1 -i "${srcdir}/firefox-version.patch"

  rm -f browser/components/shell/src/nsKDEShellService.cpp \
        browser/components/shell/src/nsKDEShellService.h \
        browser/components/shell/src/nsUnixShellService.cpp \
        browser/components/shell/src/nsUnixShellService.h \
        browser/base/content/browser-kde.xul
  rm -f toolkit/xre/nsKDEUtils.cpp \
        toolkit/xre/nsKDEUtils.h \
        uriloader/exthandler/unix/nsCommonRegistry.cpp \
        uriloader/exthandler/unix/nsCommonRegistry.h \
        uriloader/exthandler/unix/nsKDERegistry.cpp \
        uriloader/exthandler/unix/nsKDERegistry.h \
        toolkit/content/widgets/dialog-kde.xml \
        toolkit/content/widgets/preferences-kde.xml \

  install -m 644 "${srcdir}/kde.js" browser/app/profile/kde.js
  patch -Np1 -i "${srcdir}/firefox-cross-desktop.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-kde.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-browser-css.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-kde-114.patch" || return 1

  cp "${srcdir}/mozconfig" .mozconfig
  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-4.0"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
}

package() {
  cd "${srcdir}/mozilla-2.0"
  make -j1 -f client.mk DESTDIR="${pkgdir}" install

  install -m755 -d ${pkgdir}/usr/share/applications

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -d "$pkgdir/usr/share/icons/hicolor/$i/apps"
      cp other-licenses/branding/firefox/default${i/x*/}.png "$pkgdir/usr/share/icons/hicolor/$i/apps/firefox.png"
  done
  install -m644 ${srcdir}/firefox.desktop ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/firefox-safe.desktop ${pkgdir}/usr/share/applications/
}
